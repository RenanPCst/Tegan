<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_DryingPhase" Id="{3fed5c2e-00c0-41f9-b86a-23cb950cee48}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_DryingPhase

VAR_INPUT
	bExecute : BOOL;
END_VAR
VAR_OUTPUT
	bDryingDone : BOOL := FALSE;
	bDryingError : BOOL := FALSE;
END_VAR
VAR
	eDryState	: (IDLE, POSITION_M1, OPEN_AIR, WAITING_TIME, CLOSE_AIR, DONE, ERROR); //Drying Phase internal States
	
	fbSolenoidControl : FB_SolenoidControl; //Solenoid control fb instance
	tonAirDryTime : ton; //Timer to run air trought stages
	
	bStart 	: BOOL := FALSE; //Start Flag	
	bMoveM1	: BOOL := FALSE; // M2 Vertical Motor Movement Flag
	
	//HMI progress bar animation variables
	tStartTime : TIME := T#0S; // Drying start timestamp
	rElapsedTimeSec : REAL := 0.0; // Time passed in seconds
	rTotalEstimatedTimeSec : REAL := 0.0; // Total estimated time
	bProgressInitDone : BOOL := FALSE; // Estimation only once
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT bStart THEN
	eDryState := IDLE;
	bStart := bExecute;
END_IF

//HMI Progress Bar Animation
IF bProgressInitDone AND (eDryState <> IDLE) AND (eDryState <> DONE) AND (eDryState <> ERROR) THEN
	rElapsedTimeSec := TO_REAL(TIME() - tStartTime) / 1000.0;
	GVL_HMI.HMIAnimation.rLoadBarDry := MIN(100.0, rElapsedTimeSec / rTotalEstimatedTimeSec * 100.0);
END_IF

CASE eDryState OF
	
	IDLE:
		// Reset Flags
		bDryingDone := FALSE; 
		bDryingError := FALSE;
		bMoveM1 := FALSE;
        IF bStart THEN
			//Start time calculation
			tStartTime := TIME();
			rTotalEstimatedTimeSec := EstimateTotalTime();
			bProgressInitDone := TRUE;
			// Provide HMI animation details
			GVL_HMI.HMIAnimation.eIconStateDry := E_IconState.InProgress;
			GVL_HMI.HMIAnimation.sLoadTextDry := 'Starting'; 
			GVL_HMI.HMIAnimation.rLoadBarDry  := 0;	
			// Move to next step
			eDryState := POSITION_M1;
        END_IF
	
	POSITION_M1:
		IF NOT bMoveM1 THEN // M1 Movement to dry position
			bMoveM1 := GVL_Motion.aMotors[1].MoveAbsolute(
				rPosition := GVL_Settings.stMachineParam.aM1Position[3],
				rVelocity := GVL_Settings.stMachineParam.rM1Velocity,
				rAcc := GVL_Settings.stMachineParam.rM1Accleration,
				rDec := GVL_Settings.stMachineParam.rM1Deceleration
			);
		END_IF
		IF bMoveM1 THEN
			bMoveM1 := FALSE; // Reset Movement flag
			eDryState := OPEN_AIR; // Next step
		END_IF
	
	OPEN_AIR:
		fbSolenoidControl(bExecute := TRUE, eSolenoidSet := E_SolenoidSet.AIRDRY); //Set air by solenoid position
		IF fbSolenoidControl.bDone THEN
			eDryState := WAITING_TIME;
		END_IF
	
	WAITING_TIME:
		fbSolenoidControl(bExecute := FALSE); //Resets solenoid position
		tonAirDryTime(in := TRUE, pt := GVL_Recipe.stActiveRecipe.stOtherParams.tAirDryTime); //Start timer
		IF tonAirDryTime.Q THEN
			eDryState := CLOSE_AIR;
		END_IF
		
	CLOSE_AIR:
		tonAirDryTime(in := FALSE); //Timer resets
		fbSolenoidControl(bExecute := TRUE, eSolenoidSet := E_SolenoidSet.IDLE); //Set air by solenoid position
		IF fbSolenoidControl.bDone THEN
			fbSolenoidControl(bExecute := FALSE); //Resets solenoid position
			eDryState := DONE; //moving to Done Step
		END_IF
		
	DONE:
		fbSolenoidControl(bExecute := FALSE); //Resets solenoid position
		//HMI Icon animation
		GVL_HMI.HMIAnimation.eIconStateDry := E_IconState.Completed;
		GVL_HMI.HMIAnimation.sLoadTextDry  := 'Completed'; 
		GVL_HMI.HMIAnimation.rLoadBarDry   := 100;
		
		bDryingDone := TRUE; //Sucess flag
			
	ERROR:
		//HMI Icon animation
		GVL_HMI.HMIAnimation.sLoadTextDry := 'Error'; 
		GVL_HMI.HMIAnimation.rLoadBarDry  := 0;
		GVL_HMI.HMIAnimation.eIconStateDry := E_IconState.Waiting;
		
		bDryingError := TRUE; //Error Flag

END_CASE]]></ST>
    </Implementation>
    <Method Name="EstimateTotalTime" Id="{05ac8e4d-b1e3-4e8e-9944-a1369c6a61ae}">
      <Declaration><![CDATA[METHOD EstimateTotalTime : REAL
VAR
	rTotalTime : LREAL := 0.0;
    rDistance : LREAL;
    rVelocity : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// --- Motor M1 movement (sequential) ---
rDistance := ABS(GVL_Settings.stMachineParam.aM1Position[2] - GVL_Settings.stMachineParam.aM1Position[3]); //From seal position up to dry position
rVelocity := GVL_Settings.stMachineParam.rM1Velocity;
IF rVelocity > 0 THEN
    rTotalTime := rTotalTime + rDistance / rVelocity;
END_IF

// --- Solenoid switching times (estimated 0.5s per switch) ---
rTotalTime := rTotalTime + 0.5 * 3; // open_air, close_air, reset

// --- Timer duration from recipe (AirDryTime) ---
rTotalTime := rTotalTime + TO_REAL(GVL_Recipe.stActiveRecipe.stOtherParams.tAirDryTime) / 1000.0;

EstimateTotalTime := rTotalTime;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{9c3cea04-676b-42b4-ac96-ce199a63ef22}">
      <Declaration><![CDATA[METHOD Reset : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//output flags
bDryingDone  := FALSE;
bDryingError  := FALSE;

//state machine sequence
eDryState	:= IDLE;

//fb's and timers
fbSolenoidControl(bExecute := FALSE);
tonAirDryTime(In := FALSE);

//initiation flags
bStart := FALSE; 
bMoveM1 := FALSE; 

//Progress bar variables
tStartTime := T#0S;
rElapsedTimeSec := 0.0;
rTotalEstimatedTimeSec := 0.0;
bProgressInitDone := FALSE;
GVL_HMI.HMIAnimation.rLoadBarDry := 0;

IF NOT bDryingDone AND NOT bDryingError THEN
	Reset := TRUE;
	RETURN;
ELSE
	Reset := FALSE;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_DryingPhase">
      <LineId Id="34" Count="2" />
      <LineId Id="9" Count="0" />
      <LineId Id="141" Count="0" />
      <LineId Id="136" Count="0" />
      <LineId Id="138" Count="2" />
      <LineId Id="137" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="64" Count="1" />
      <LineId Id="79" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="133" Count="1" />
      <LineId Id="132" Count="0" />
      <LineId Id="67" Count="4" />
      <LineId Id="76" Count="0" />
      <LineId Id="46" Count="1" />
      <LineId Id="44" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="83" Count="4" />
      <LineId Id="81" Count="1" />
      <LineId Id="48" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="88" Count="1" />
      <LineId Id="58" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="95" Count="2" />
      <LineId Id="59" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="60" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="101" Count="1" />
      <LineId Id="104" Count="0" />
      <LineId Id="43" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="105" Count="0" />
      <LineId Id="62" Count="0" />
      <LineId Id="109" Count="0" />
      <LineId Id="106" Count="2" />
      <LineId Id="55" Count="0" />
      <LineId Id="110" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="112" Count="1" />
      <LineId Id="116" Count="0" />
      <LineId Id="118" Count="0" />
      <LineId Id="117" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="124" Count="0" />
      <LineId Id="122" Count="1" />
      <LineId Id="121" Count="0" />
      <LineId Id="119" Count="0" />
      <LineId Id="125" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="40" Count="0" />
    </LineIds>
    <LineIds Name="FB_DryingPhase.EstimateTotalTime">
      <LineId Id="11" Count="12" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_DryingPhase.Reset">
      <LineId Id="27" Count="0" />
      <LineId Id="15" Count="1" />
      <LineId Id="28" Count="0" />
      <LineId Id="18" Count="1" />
      <LineId Id="29" Count="0" />
      <LineId Id="20" Count="2" />
      <LineId Id="26" Count="0" />
      <LineId Id="23" Count="1" />
      <LineId Id="11" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="6" Count="4" />
      <LineId Id="33" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="34" Count="0" />
      <LineId Id="36" Count="2" />
      <LineId Id="35" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>