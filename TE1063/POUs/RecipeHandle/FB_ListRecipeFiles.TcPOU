<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_ListRecipeFiles" Id="{c0094d53-68e1-453f-9c4e-bed934c9b18b}" SpecialFunc="None">
    <Declaration><![CDATA[// FB_ListRecipeFiles: Scans the recipe folder and fills GVL_Recipe.aAvailableFiles with metadata from JSON files
FUNCTION_BLOCK FB_ListRecipeFiles
VAR_INPUT
	bExecute : BOOL; // Start the file listing process
END_VAR
VAR_OUTPUT
	bBusy       : BOOL; // Indicates the block is busy
    bError      : BOOL; // Indicates an error occurred
    nErrId      : UDINT; // Error code if any
	bDone   	: BOOL;

END_VAR
VAR
	fbEnumFiles     : FB_EnumFindFileList;
    fbFileTime      : FB_FileTime64ToTzSpecificLocalTime;
    fbHeaderHandler : FB_RecipeHeaderHandler;

    aFindBuffer     : ARRAY[0..49] OF ST_FindFileEntry;
    nFoundFiles     : UDINT;
    iFileIndex      : INT;
    iStep           : INT;
    bExecutePrev    : BOOL;
    bStart          : BOOL;
    bHeaderHandlerReset : BOOL;
    bHeaderHandlerExecute : BOOL;
	
	i : INT;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[bStart := NOT bExecutePrev AND bExecute;
bExecutePrev := bExecute;

IF NOT bExecute THEN
	bBusy := FALSE;
  	bDone := FALSE;
	bError := FALSE;
	nErrId := 0;
END_IF

CASE iStep OF

0:
    IF bStart THEN
        bBusy := TRUE;
        bDone := FALSE;
        bError := FALSE;
        nErrId := 0;
        iFileIndex := 0;
        bHeaderHandlerReset := FALSE;
        bHeaderHandlerExecute := FALSE;

        fbEnumFiles(
            sNetId := '',
            sPathName := 'C:/TwinCAT/Recipes/*.json',
            eCmd := eEnumCmd_First,
            pFindList := ADR(aFindBuffer),
            cbFindList := SIZEOF(aFindBuffer),
            bExecute := TRUE
        );

        iStep := 10;
    END_IF

10:
    fbEnumFiles(bExecute := FALSE);
    IF NOT fbEnumFiles.bBusy THEN
        IF fbEnumFiles.bError THEN
            bError := TRUE;
            nErrId := fbEnumFiles.nErrId;
            iStep := 100;
        ELSE
            nFoundFiles := fbEnumFiles.nFindFiles;
            IF nFoundFiles > 0 THEN
                iStep := 20;
            ELSE
                bDone := TRUE;
                iStep := 100;
            END_IF
        END_IF
    END_IF

// 20 - Processa arquivo iFileIndex
20:
    IF iFileIndex < TO_INT(nFoundFiles) AND iFileIndex <= 49 THEN
        // Preenche campos básicos do arquivo
        GVL_HMI.aAvailableFiles[iFileIndex].sFileName := aFindBuffer[iFileIndex].sFileName;
        fbFileTime(
            in := FILETIME_TO_FILETIME64(aFindBuffer[iFileIndex].lastWriteTime),
            tzInfo := WEST_EUROPE_TZI
        );
        GVL_HMI.aAvailableFiles[iFileIndex].dtLastModified := FILETIME64_TO_DT(fbFileTime.out);

        // Vai para o step de reset do FB_HeaderHandler
        iStep := 21;
    ELSE
        bDone := TRUE;
        iStep := 100;
    END_IF

// 21 - Reset do FB_HeaderHandler (chamada com bExecute := FALSE por um ciclo)
21:
    fbHeaderHandler(bExecute := FALSE);
    IF NOT fbHeaderHandler.bBusy AND NOT fbHeaderHandler.bDone THEN
        iStep := 22;
    END_IF

// 22 - Chama o FB_HeaderHandler para ler o header do arquivo atual
22:
    fbHeaderHandler(
        sFileName := CONCAT('C:/TwinCAT/Recipes/', aFindBuffer[iFileIndex].sFileName),
        iTargetIndex := iFileIndex,
        bExecute := TRUE
    );
    IF NOT fbHeaderHandler.bBusy THEN
        IF fbHeaderHandler.bError THEN
            bError := TRUE;
            nErrId := fbHeaderHandler.nErrId;
            iStep := 100;
        ELSIF fbHeaderHandler.bDone THEN
            iStep := 23;
        END_IF
    END_IF

23: // Dummy step — aguarda 1 ciclo antes de liberar novo arquivo
    fbHeaderHandler(bExecute := FALSE);
    iFileIndex := iFileIndex + 1;
    iStep := 20;

100:
    bBusy := FALSE;
    IF NOT bExecute THEN
        iStep := 0;
    END_IF
END_CASE ]]></ST>
    </Implementation>
    <LineIds Name="FB_ListRecipeFiles">
      <LineId Id="1731" Count="1" />
      <LineId Id="1904" Count="0" />
      <LineId Id="1903" Count="0" />
      <LineId Id="1907" Count="2" />
      <LineId Id="1905" Count="1" />
      <LineId Id="1733" Count="42" />
      <LineId Id="1835" Count="0" />
      <LineId Id="1953" Count="14" />
      <LineId Id="1851" Count="3" />
      <LineId Id="1923" Count="2" />
      <LineId Id="1855" Count="0" />
      <LineId Id="1857" Count="2" />
      <LineId Id="1999" Count="13" />
      <LineId Id="1919" Count="3" />
      <LineId Id="1828" Count="6" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>